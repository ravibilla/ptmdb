Names Default To Here( 1 );

ns = ::GetAddinNamespaceFn();
Addin_Name = ns:AddinArray["name"];
//::LogUsageFn( "EIS_Quad_Summary_By_Date" );

// Include files
Include( "$ADDIN_HOME(com.ensurge.tech)\Functions\DateBoxFn.jsl" );
Include( "$ADDIN_HOME(com.ensurge.tech)\Functions\FormatFEEISDataColumnsFn.jsl" );
Include( "$ADDIN_HOME(com.ensurge.tech)\Functions\FormatBEEISDataColumnsFn.jsl" );
Include( "$ADDIN_HOME(com.ensurge.tech)\Functions\RenameEISDataColumnsFn.jsl" );
Include( "$ADDIN_HOME(com.ensurge.tech)\Functions\FormatEISDataColumnsFn.jsl" );

// Create a modal dialog
New Window( "EIS Summary data by Date",
	<<Modal,
	V List Box(
		Text Box( "Query EIS Summary data by Date", <<set font size( 11 ), <<set font style( "bold" ) ),
		Text Box( "" ),
		H List Box(
			Panel Box( "Select Date Range:",
				V List Box(
					Spacer Box(),
					H List Box(
						Text Box( "From: ", "" ),
						start_date = ::DateBoxFn( Today() - in weeks(1) ), // Set to 1 week ago
						Text Box( "  ", "" ),
						Text Box( "To: ", "" ),
						end_date = ::DateBoxFn( Today() ),
						Text Box( "  ", "" )
					)
				)
			)
		),
		H List Box(
			Spacer Box( size( 220, 0 ) ),
			Button Box( "OK",
				sDate = Format((start_date << Get), "yyyy-mm-dd");
				eDate = Format((end_date << Get), "yyyy-mm-dd");

				// Query Frontend EIS Summary data
				queryString = "CALL spGetFrontendEISDataSummaryByDate('" || sDate || "', '" || eDate || "');";
				dbCon = Create Database Connection( ::GetDSNCredentialsFn("feeis") );
				dtFE = Execute SQL( dbCon, queryString, "FE-EIS Summary by Date" );
				dtFE << Show Window(0);
				Close Database Connection( dbCon );
	
				// Query Backend EIS Summary data
				queryString = "EXECUTE spGetBackendEISDataSummaryByDate '" || Char( sDate ) || "', '" || Char( eDate ) || "';";
				dbCon = Create Database Connection( ::GetDSNCredentialsFn("beeis") );
				dtBE = Execute SQL( dbCon, queryString, "BE-EIS Summary by Date" );
				dtBE << Show Window(0);
				Close Database Connection( dbCon );
				
				// Fix column formats
				dtFE = ::FormatFEEISDataColumnsFn( dtFE );
				dtBE = ::FormatBEEISDataColumnsFn( dtBE );

				// Concatenate FE & BE data
				dtConcat = New Table("Concatenated FE & BE EIS Summary by Date");
				dtConcat << Concatenate( dtFE, dtBE, "Append to first table");
				Close( dtFE, NoSave );
				Close( dtBE, NoSave );
				
				// Join with Bin Names
				dtBin = Open("$ADDIN_HOME(com.ensurge.tech)\Files\EISBinNames.jmp");
				dtBinJoined = dtConcat  << Join(
							With( Data Table("EISBinNames")),
							By Matching Columns( :Bin = :Bin ),
							Drop multiples( 0, 0 ),
							Include Nonmatches( 0, 0 ),
							Merge Same Name Columns( 1 ), 
							Preserve main table order( 1 ),
							output table("FE & BE EIS Summary by Date")
							);
							
				Close( dtBin, NoSave );
				Close( dtConcat, NoSave );

				// Rename columns
				dtBinJoined = ::RenameEISDataColumnsFn( dtBinJoined );

				// Format columns
				dtBinJoined = ::FormatEISDataColumnsFn( dtBinJoined );

				// Set window size
				dtBinJoined << Set Window Size(1400, 850);
				
				// Close all child tables and reports without saving
				dtBinJoined << onclose( Function( {a}, Close( (a << Child) << GetDataTable, NoSave ); 1; ) );
				
			)
		)
	)
);

