Names Default To Here( 1 );

ns = ::GetAddinNamespaceFn();
Addin_Name = ns:AddinArray["name"];
//::LogUsageFn( "EIS_Quad_RAW_By_Lot" );

// Include files
Include( "$ADDIN_HOME(com.ensurge.tech)\Functions\FormatFEEISDataColumnsFn.jsl" );
Include( "$ADDIN_HOME(com.ensurge.tech)\Functions\FormatBEEISDataColumnsFn.jsl" );
Include( "$ADDIN_HOME(com.ensurge.tech)\Functions\RenameEISDataColumnsFn.jsl" );
Include( "$ADDIN_HOME(com.ensurge.tech)\Functions\FormatEISDataColumnsFn.jsl" );

// Create a modal dialog
New Window( "EIS RAW Data by Lot",
	<<Modal,
	V List Box(
		Text Box( "Query EIS RAW Data by Lot", <<set font size( 11 ), <<set font style( "bold" ) ),
		Text Box( "" ),
		H List Box(
			Panel Box( "Enter Lot ID:",
				V List Box(
					Spacer Box(),
					H List Box(
						lot_id = Text Edit Box( "", Set Width( 80 ) )
					)
				)
			)
		),
		H List Box(
			Spacer Box( size( 220, 0 ) ),
			Button Box( "OK",
				
				LotID = lot_id << Gettext;

				//Exit script if user doesn't enter lot id
				If( LotID == "",
					Try( NaN, Throw( New Window( "Input Error", <<Modal, tb1 = Text Box( "Please enter a lot id." ) ) ) )
				);

				// Query Frontend EIS RAW data
				queryString = "CALL spGetFrontendEISDataByLot('" || LotID || "');";
				dbCon = Create Database Connection( ::GetDSNCredentialsFn("feeis") );
				dtFE = Execute SQL( dbCon, queryString, "FE-EIS RAW by Lot" );
				dtFE << Show Window(0);
				Close Database Connection( dbCon );
	
				// Query Backend EIS RAW data
				queryString = "EXECUTE spGetBackendEISDataByLot '" || LotID || "';";
				dbCon = Create Database Connection( ::GetDSNCredentialsFn("beeis") );
				dtBE = Execute SQL( dbCon, queryString, "BE-EIS RAW by Lot" );
				dtBE << Show Window(0);
				Close Database Connection( dbCon );
				
				// Fix column formats
				dtFE = ::FormatFEEISDataColumnsFn( dtFE );
				dtBE = ::FormatBEEISDataColumnsFn( dtBE );

				// Concatenate FE & BE data
				dtConcat = New Table("Concatenated FE & BE EIS RAW by Lot");
				dtConcat << Concatenate( dtFE, dtBE, "Append to first table");
				Close( dtFE, NoSave );
				Close( dtBE, NoSave );

				// Join with Bin Names
				dtBin = Open("$ADDIN_HOME(com.ensurge.tech)\Files\EISBinNames.jmp");
				dtBinJoined = dtConcat  << Join(
							With( Data Table("EISBinNames")),
							By Matching Columns( :Bin = :Bin ),
							Drop multiples( 0, 0 ),
							Include Nonmatches( 0, 0 ),
							Merge Same Name Columns( 1 ), 
							Preserve main table order( 1 ),
							output table("FE & BE EIS RAW by Lot")
							);
							
				Close( dtBin, NoSave );
				Close( dtConcat, NoSave );

				// Rename columns
				dtBinJoined = ::RenameEISDataColumnsFn( dtBinJoined );

				// Format columns
				dtBinJoined = ::FormatEISDataColumnsFn( dtBinJoined );

				// Set window size
				dtBinJoined << Set Window Size(1400, 850);
				
				// Close all child tables and reports without saving
				dtBinJoined << onclose( Function( {a}, Close( (a << Child) << GetDataTable, NoSave ); 1; ) );
				
			)
		)
	)
);

